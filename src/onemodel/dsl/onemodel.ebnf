@@grammar::OneModel


# TatSu by default defines newline as whitespace but onemodel syntax
# uses newlines as separators. So we have removed the intro from the
# whitespace characters in TatSu.
@@whitespace::/[\t ]+/


# This allows us to use comments like in python.
@@eol_comments::/#.*?$/


# Keywords reserved.
@@keyword:: species parameter reaction end rule function


start
  = 
  {statements} $
  ;


statements
  =
  {newline} @:statement {{newline}+ @:statement} {newline}
  ;


statement
  = 
  expr
  ;


expr
  =
  | assign_variable
  | comp_expr
  ;


assign_variable::AssignVariable
  =
  'let' name:identifier '=' value:expr
  ;


comp_expr
  =
  arith_expr
  ;


arith_expr
  =
  term
  ;


term
  =
  factor
  ;


factor
  =
  power
  ;


power
  =
  call
  ;


call::Call
  =
  | value:atom '(' [args:expr {',' args:expr}] ')'
  | next:atom
  ;


atom
  = 
  | float
  | integer
  | string
  | access_identifier
  | function_definition
  ;


# Definition of integers.
integer::Integer
  = 
  value:/\d+/
  ;


# Definition of floats.
float::Float
  =
  | value:/\d*(?:\.\d+)?(?:e(?:\+|\-)\d+)/  # Scientific notation.
  | value:/\d*\.\d+/                        # Regular floats.
  ;


# Definition of strings.
string::String
  = 
  "'" value:/[^\\\r\n\f']*/ "'"
  ;


# Access the value of an identifier.
access_identifier::AccessIdentifier
  = 
  name:identifier
  ;


function_definition::FunctionDefinition
  =
  'function' name:identifier '(' [args:identifier {',' args:identifier}] ')' newline
  {newline | body:statement}
  'end'
  ;

# Definition of identifiers
# Identifiers cannot have a keyword name.
@name
identifier::str
  =
  /(?!\d)\w+/ 
  ;


newline 
  = 
  | ';'
  | /\n/
  ;
