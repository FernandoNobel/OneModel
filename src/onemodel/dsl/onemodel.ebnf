@@grammar::OneModel


# TatSu by default defines newline as whitespace but onemodel syntax
# uses newlines as separators. So we have removed the intro from the
# whitespace characters in TatSu.
@@whitespace::/[\t ]+/


# This allows us to use comments like in python.
@@eol_comments::/#.*?$/


# Keywords reserved.
@@keyword:: species parameter 


start
  = 
  {newline | @:statement} $
  ;


statement
  = 
  | 'species' @:species
  | 'parameter' @:parameter
  | 'reaction' @:reaction
  | function
  | atom
  ;


species::Species
  = 
  name:identifier '=' value:atom
  ;


parameter::Parameter
  = 
  name:identifier '=' value:atom
  ;


# S1 + S2 -> S1 + S3; k1*S1(S1+h1)
reaction::Reaction
  =
  {'+' | reactants:identifier} 
  '->' 
  {'+' | products:identifier} 
  ';' 
  kinetic_law:formula
  ;


function
  =
  printSBML
  ;


printSBML::PrintSBML
  =
  'printSBML'
  ;


atom
  = 
  | float
  | integer
  | string
  ;


# Definition of integers.
integer::int
  = 
  /\d+/
  ;


# Definition of floats.
float::float
  =
  | /\d*(?:\.\d+)?(?:e(?:\+|\-)\d+)/  # Scientific notation.
  | /\d*\.\d+/                        # Regular floats.
  ;


# Definition of strings.
string::str
  = 
  "'" @:/[^\\\r\n\f']*/ "'"
  ;


# Definition of identifiers
# Identifiers cannot have a keyword name.
@name
identifier::str
  =
  /(?!\d)\w+/ 
  ;


formula::str
  =
  /[^\\\r\n\f'\;]*/
  ;


newline 
  = 
  | ';'
  | /\n/
  ;
